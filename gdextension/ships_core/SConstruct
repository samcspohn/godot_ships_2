#!/usr/bin/env python
import os
import subprocess
import sys
import tempfile


def spawn_with_response_file(sh, escape, cmd, args, env):
    """Custom spawn that uses response files for ar commands with long argument lists."""
    cmdline = " ".join(args)

    # Only use response file for ar commands with very long argument lists
    if len(cmdline) > 60000 and args[0].endswith("ar"):
        with tempfile.NamedTemporaryFile(mode="w", delete=False, suffix=".rsp") as f:
            f.write("\n".join(args[1:]))
            rsp_file = f.name
        try:
            result = subprocess.call([args[0], "@" + rsp_file], env=env)
        finally:
            os.unlink(rsp_file)
        return result
    else:
        return subprocess.call(cmdline, shell=True, env=env)


# Create base environment with custom spawn before importing godot-cpp
env = Environment(tools=["default"], PLATFORM="")
env["SPAWN"] = spawn_with_response_file
Export("env")

env = SConscript("../godot-cpp/SConstruct")

env["SPAWN"] = spawn_with_response_file

env.Append(CPPPATH=["src/"])
sources = Glob("src/*.cpp")

if env["platform"] == "macos":
    library = env.SharedLibrary(
        "../../bin/libships_core.{}.{}.framework/libships_core.{}.{}".format(
            env["platform"], env["target"], env["platform"], env["target"]
        ),
        source=sources,
    )
elif env["platform"] == "ios":
    if env["ios_simulator"]:
        library = env.StaticLibrary(
            "../../bin/libships_core.{}.{}.simulator.a".format(
                env["platform"], env["target"]
            ),
            source=sources,
        )
    else:
        library = env.StaticLibrary(
            "../../bin/libships_core.{}.{}.a".format(env["platform"], env["target"]),
            source=sources,
        )
else:
    library = env.SharedLibrary(
        "../../bin/libships_core{}{}".format(env["suffix"], env["SHLIBSUFFIX"]),
        source=sources,
    )

Default(library)
